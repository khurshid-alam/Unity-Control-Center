AC_INIT(control-center)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(control-center, 1.3.10)

AM_MAINTAINER_MODE

dnl
dnl let applications configure for gnome
dnl
gnome_cv_use_gnome=yes

AM_ACLOCAL_INCLUDE(macros)

AM_PROG_XML_I18N_TOOLS
GNOME_INIT
GNOME_COMPILE_WARNINGS

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AM_PROG_LEX
AC_PROG_YACC

dnl utility conditional
AM_CONDITIONAL(FALSE, test "x" = "y")

ALL_LINGUAS="az ca cs da de el en_GB es et fi fr ga gl hr hu it ja ko lt nl no pl pt pt_BR ro ru sk sl sv tr uk zh_CN.GB2312 zh_TW.Big5"
AM_GNOME_GETTEXT

GNOME_XML_CHECK
XML_CFLAGS=`gnome-config --cflags xml`
AC_SUBST(XML_CFLAGS)

AM_PATH_LIBGLADE(,,"gnome")

AC_CHECK_HEADERS(dlfcn.h dl.h)
AC_CHECK_LIB(dl, dlopen, DL_LIB="-ldl",[
AC_CHECK_LIB(dld, shl_load, DL_LIB="-ldld",[
AC_CHECK_FUNCS(dlopen, DL_LIB="",
AC_MSG_ERROR(Dynamic linking is not available on this platform.  Some
apps, like panel, will not run properly.))
])])
AC_SUBST(DL_LIB)

AC_CHECK_FUNCS(usleep)
AC_CHECK_FUNCS(putenv,[AC_DEFINE(HAVE_PUTENV)])
AC_CHECK_FUNCS(setenv,[AC_DEFINE(HAVE_SETENV)])

dnl keyboard-properties-capplet
AC_CHECK_HEADERS(X11/extensions/xf86misc.h, XF86MISC_LIBS="-lXxf86misc")
AC_SUBST(XF86MISC_LIBS)

AM_PATH_LIBGLADE(,AC_MSG_ERROR([You must have LibGlade installed.]), gnome)

dnl esd-manager
have_libesd=no
AM_PATH_ESD(,have_libesd=yes,
[AC_MSG_WARN([*** \`esd-manager' will not be built ***])])
AM_CONDITIONAL(HAVE_LIBESD, test yes = $have_libesd)
if test "$have_libesd" = yes; then
	AC_DEFINE(HAVE_ESD)
	AC_DEFINE(HAVE_LIBESD)
fi

dnl session-properties
AM_CONDITIONAL(SESSION, test "$GNOME_HAVE_SM" = true)

AM_PATH_IMLIB(1.8.2, , [
echo "ERROR: Needs a system with Imlib 1.8.2 or higher"
echo "You can obtain it from:"
echo "ftp://ftp.enlightenment.org/pub/enlightenment/"
echo "ftp://www.rasterman.com/pub/enlightenment/"
echo "ftp://ftp.labs.redhat.com/pub/imlib/"
AC_MSG_ERROR([Fatal Error: no Imlib detected.])])

CFLAGS="-O2 -g -Wall $CFLAGS"

dnl =============================================
dnl END : Variables for config_archiverConf.sh.in
dnl =============================================

AC_PATH_PROG(PKGCONFIG, pkg-config, no)
if test "$PKGCONFIG" = no ; then
	AC_MSG_ERROR(pkg-config was not found. Please install version 0.8.0 or newer from http://www.freedesktop.org/software.)
fi

AC_MSG_CHECKING(for pkg-config >= 0.8.0)
vers=`$PKGCONFIG --version | awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 1000 + $3}'`
if test "$vers" -ge 8000; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(You need at least pkg-config 0.8.0 or greater for this version of control-center. Please install a newer version from http://www.freedesktop.org/software.)
fi

AC_MSG_CHECKING(for bonobo-conf >= 0.7)
vers=`$PKGCONFIG --version | awk 'BEGIN { FS = "."; } { print $1 * 1000000 + $2 * 1000 + $3}'`
if test "$vers" -ge 7000; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(You need at least bonobo-conf 0.7 or greater for this version of control-center.)
fi

capplet_modules="libcapplet libglade-gnome gdk_pixbuf bonobo_conf $ARCHIVER_MODULE"

PKG_CHECK_MODULES(CAPPLET, $capplet_modules)
PKG_CHECK_MODULES(BG_CAPPLET, $capplet_modules gdk_pixbuf_xlib)
PKG_CHECK_MODULES(SCREENSAVER_CAPPLET, $capplet_modules gal)
dnl PKG_CHECK_MODULES(ROLLBACK_CAPPLET, $capplet_modules)
PKG_CHECK_MODULES(GNOMECC, gnomeui libglade xml gdk_pixbuf gnomecanvaspixbuf bonobo bonobox gtkhtml)
PKG_CHECK_MODULES(RMHELPER, gnomeui gnomecanvaspixbuf)
PKG_CHECK_MODULES(ARCHIVER, bonobo bonobo_conf xml)
PKG_CHECK_MODULES(MONIKER, bonobo bonobo_conf xml)
PKG_CHECK_MODULES(CONFIG_ARCHIVER, gnome xml)

GNOMECC_CAPPLETS_CFLAGS="${CAPPLET_CFLAGS} -I\$(top_srcdir)/intl -I\$(top_srcdir)/ -DG_LOG_DOMAIN=\"\\\"keyboard-properties\\\"\" -DGLADE_DATADIR=\"\\\"${datadir}/control-center\\\"\" -DGNOMELOCALEDIR=\"\\\"${datadir}/locale\\\"\"  "

AC_DEFINE_UNQUOTED(GNOMECC_ICONS_DIR, "${datadir}/${PACKAGE}/icons")
GNOMECC_ICONS_DIR=""${datadir}/${PACKAGE}/icons""
AC_SUBST(GNOMECC_ICONS_DIR)

AC_SUBST(GNOMECC_CAPPLETS_CFLAGS)

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

GNOMECC_CAPPLETS_CLEANFILES="\$(desktop) \$(desktop).in"
GNOMECC_CAPPLETS_EXTRA_DIST="ChangeLog \$(desktop).in.in \$(desktop).in \$(desktop) \$(cappletname)-capplet.png \$(cappletname)-properties.glade"

AC_SUBST(GNOMECC_CAPPLETS_EXTRA_DIST)
AC_SUBST(GNOMECC_CAPPLETS_CLEANFILES)
dnl =======================================
dnl Variables for config_archiverConf.sh.in
dnl =======================================




dnl ==============================================
dnl End : Macro for the common code
dnl ==============================================
AC_DEFUN(AC_PROG_GNOMECC_CAPPLETS,
[
GNOMECC_CAPPLETS_DESKTOP_IN_RULE='iconsdir = $(GNOMECC_ICONS_DIR) \
icons_DATA = $(cappletname)-capplet.png \
Gladedir = $(datadir)/control-center-data \
Glade_DATA = $(cappletname)-properties.glade \
\
$(desktop).in: \%.desktop.in: \%.desktop.in.in\
	sed s#Icon=.\*#Icon=$(GNOMECC_ICONS_DIR)/${cappletname}-capplet.png# < $< \> ${cappletname}.desktop.in\
\
install-data-local:\
	$(mkinstalldirs) $(datadir)/control-center/capplets\
	$(INSTALL_DATA) $(srcdir)/$(desktop) $(datadir)/control-center/capplets/$(desktop)\
install-data-am: install-data-local'
AC_DIVERT_PUSH(AC_DIVERSION_SED)dnl
s%@GNOMECC_CAPPLETS_DESKTOP_IN_RULE@%[$]GNOMECC_CAPPLETS_DESKTOP_IN_RULE%g
AC_DIVERT_POP()dnl
# Redirect the config.log output again, so that the ltconfig log is not
# clobbered by the next message.
exec 5>>./config.log
])
AC_PROG_GNOMECC_CAPPLETS
dnl ==============================================
dnl End : Macro for the common code
dnl ==============================================


CONFIG_ARCHIVER_LIBDIR='-L${libdir}'
CONFIG_ARCHIVER_LIBS="$CONFIG_ARCHIVER_LIBS -lconfig_archiver"
CONFIG_ARCHIVER_INCLUDEDIR="$CONFIG_ARCHIVER_CFLAGS -I${includedir}"

AC_SUBST(CONFIG_ARCHIVER_LIBDIR)
AC_SUBST(CONFIG_ARCHIVER_LIBS)
AC_SUBST(CONFIG_ARCHIVER_INCLUDEDIR)


AC_OUTPUT([
control-center.spec
Makefile
po/Makefile.in
archiver/Makefile
control-center/Makefile
capplets/Makefile
capplets/common/Makefile
capplets/background/Makefile
capplets/default-applications/Makefile
capplets/desktop-links/Makefile
capplets/desktop-links/Sawfish/Makefile
capplets/keyboard/Makefile
capplets/mouse/Makefile
dnl capplets/rollback/Makefile
capplets/screensaver/Makefile
capplets/screensaver/screensavers/Makefile
capplets/sound/Makefile
intl/Makefile])
