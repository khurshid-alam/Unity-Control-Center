AC_INIT([gnome-control-center], [2.23.0],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=gnome-control-center])

AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR([shell])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([1.9 no-dist-gzip dist-bzip2 tar-ustar])
AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_C_O
AC_STDC_HEADERS
AM_PROG_LIBTOOL

# Use the GNOME documentation framework
GNOME_DOC_INIT

m4_define(gnomevfs_minver, 2.0.0)
m4_define(fontconfig_minver, 1.0.0)
m4_define(xft_minver, 2.1.2)
m4_define(gtk_minver, 2.11.6)
m4_define(libbonobo_minver, 2.0.0)
m4_define(libgnomeui_minver, 2.2.0)
m4_define(nautilus_minver, 2.6.0)

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac
fi
changequote([,])dnl

# Internationalization support

IT_PROG_INTLTOOL([0.35.0])

GETTEXT_PACKAGE=gnome-control-center-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])

AM_GLIB_GNU_GETTEXT

GNOME_COMMON_INIT
GNOME_DEBUG_CHECK
GNOME_COMPILE_WARNINGS([maximum])
GNOME_MAINTAINER_MODE_DEFINES

AC_PATH_XTRA
x_libs="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"

AC_CHECK_FUNC(XScreenSaverQueryInfo, [
  TYPING_BREAK="typing-break"], [
  save_LIBS=$LIBS
  LIBS="$LIBS $X_PRE_LIBS $X_LIBS -lXss -lXext -lX11"
  AC_CHECK_LIB(Xss, XScreenSaverQueryInfo, [
    TYPING_BREAK="typing-break"
    SCREENSAVER_LIBS="$X_PRE_LIBS $X_LIBS -lXss -lXext -lX11"], [
    LIBS=$save_LIBS
    LIBS="$LIBS $X_PRE_LIBS $X_LIBS -lXext -lX11"
    AC_CHECK_LIB(Xext, XScreenSaverQueryInfo, [
      TYPING_BREAK="typing-break"
      SCREENSAVER_LIBS="$X_PRE_LIBS $X_LIBS -lXext -lX11"],[])])
  LIBS=$save_LIBS])
AC_SUBST(TYPING_BREAK)
AC_SUBST(SCREENSAVER_LIBS)

dnl keyboard-properties-capplet
savecppflags=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
AC_CHECK_HEADERS([X11/Xlib.h])
AC_CHECK_LIB(Xxf86misc, XF86MiscQueryExtension, [
  AC_CHECK_HEADERS([X11/extensions/xf86misc.h], [XF86MISC_LIBS="-lXxf86misc"],[],
[#if HAVE_X11_XLIB_H
#include <X11/Xlib.h>
#endif
])])
AC_SUBST(XF86MISC_LIBS)
AC_CHECK_HEADERS(X11/extensions/XKB.h)
CPPFLAGS=$savecppflags

dnl ==============================================
dnl Check that we meet the  dependencies
dnl ==============================================

ENABLE_DYNAMIC_LIBSLAB=0
AM_CONDITIONAL(ENABLE_DYNAMIC_LIBSLAB, test "x$ENABLE_DYNAMIC_LIBSLAB" = "x1")

COMMON_MODULES="gtk+-2.0 >= 2.11.6 dnl
 gio-2.0 dnl
 gconf-2.0 dnl
 libgnomeui-2.0 >= 2.2.0 dnl
 libglade-2.0 >= 2.0.0 dnl
 libbonobo-2.0 dnl
 libbonoboui-2.0 dnl
 gnome-desktop-2.0 >= 2.21.4"
PKG_CHECK_MODULES(LIBSLAB, $COMMON_MODULES gnome-desktop-2.0 librsvg-2.0 libgnome-menu pango eel-2.0)
PKG_CHECK_MODULES(CAPPLET, $COMMON_MODULES)
PKG_CHECK_MODULES(GNOMECC, $COMMON_MODULES libgnome-menu >= 2.10.1 libpanelapplet-2.0)
PKG_CHECK_MODULES(GNOME, libgnomeui-2.0)
PKG_CHECK_MODULES(DBUS, dbus-1 dbus-glib-1)
PKG_CHECK_MODULES(VFS_CAPPLET, $COMMON_MODULES gnome-vfs-module-2.0 gnome-vfs-2.0)
PKG_CHECK_MODULES(GNOME_DESKTOP, gnome-desktop-2.0)
PKG_CHECK_MODULES(DEFAULT_APPLICATIONS_CAPPLET, libxml-2.0)
PKG_CHECK_MODULES(METACITY, libmetacity-private >= 2.23.1)
PKG_CHECK_MODULES(TYPING, glib-2.0 > 2.11 gconf-2.0 gtk+-2.0)
PKG_CHECK_MODULES(LIBBACKGROUND, glib-2.0 gobject-2.0 gdk-2.0 gconf-2.0)
PKG_CHECK_MODULES(LIBSOUNDS, gio-2.0 libgnome-2.0 libgnomeui-2.0)
PKG_CHECK_MODULES(GSD_DBUS, gnome-settings-daemon)

gtk_lib_dir=`$PKG_CONFIG --variable libdir gtk+-2.0`
gtk_binary_version=`$PKG_CONFIG --variable gtk_binary_version gtk+-2.0`
GTK_ENGINE_DIR="$gtk_lib_dir/gtk-2.0/$gtk_binary_version/engines"
AC_SUBST(GTK_ENGINE_DIR)

dnl
dnl Check for Xft version 2; we build in extra functionality to the font capplet
dnl when we have it.
dnl
xft_modules=
if $PKG_CONFIG --exists xft ; then
  xft_modules="xft"
  AC_DEFINE(HAVE_XFT2,,[Define if Xft functionality is available])
fi

PKG_CHECK_MODULES(FONT_CAPPLET, $COMMON_MODULES $xft_modules)

PKG_CHECK_MODULES(AT_CAPPLET, $COMMON_MODULES)

dnl
dnl Check for XRandR, needed for display capplet
dnl

have_randr=no
AC_CHECK_LIB(Xrandr, XRRUpdateConfiguration,
  [AC_CHECK_HEADER(X11/extensions/Xrandr.h,
     have_randr=yes
     RANDR_LIBS="-lXrandr -lXrender"
     AC_DEFINE(HAVE_RANDR, 1, [Define if Xrandr extension is available]),
	  :, [#include <X11/Xlib.h>])], : ,
       -lXrandr -lXrender $x_libs)
AM_CONDITIONAL(HAVE_RANDR, [test $have_randr = yes])

PKG_CHECK_MODULES(DISPLAY_CAPPLET, $COMMON_MODULES)

DISPLAY_CAPPLET_LIBS="$DISPLAY_CAPPLET_LIBS $RANDR_LIBS"

CAPPLET_LIBS="$CAPPLET_LIBS $x_libs"
GNOMECC_LIBS="$GNOMECC_LIBS $x_libs"

dnl
dnl Check for XCursor support.  If it exists, then we compile the
dnl mouse capplet with support for it turned on
dnl
have_xcursor=no
PKG_CHECK_MODULES(XCURSOR, [
	xcursor
	],
	have_xcursor=yes
	AC_DEFINE(HAVE_XCURSOR, 1, [Define if the Xcursor extension is available]),
	have_xcursor=no)
AM_CONDITIONAL(HAVE_XCURSOR, [test $have_xcursor=yes])
AC_SUBST(XCURSOR_CFLAGS)
AC_SUBST(XCURSOR_LIBS)

if test x$have_xcursor = xyes; then
  AC_MSG_CHECKING([for XCURSOR cursor location])
  XCURSOR_ICONDIR=$($PKG_CONFIG --variable=icondir xcursor)
  if test "x$XCURSOR_ICONDIR" != x; then
    AC_DEFINE_UNQUOTED(XCURSOR_ICONDIR, "${XCURSOR_ICONDIR}", [Define to the xcursor icon path])
  else
    XCURSOR_ICONDIR=none
  fi
  AC_MSG_RESULT([${XCURSOR_ICONDIR}])
fi

CAPPLET_LIBS="$CAPPLET_LIBS $XCURSOR_LIBS"

dnl ==============
dnl gswitchit
dnl ==============
PKG_CHECK_MODULES(LIBGNOMEKBD, [libgnomekbd >= 2.21.4.1 libxklavier >= 3.3])
AC_SUBST(LIBGNOMEKBD_CFLAGS)
AC_SUBST(LIBGNOMEKBD_LIBS)

PKG_CHECK_MODULES(LIBGNOMEKBDUI, [libgnomekbdui >= 2.21])
AC_SUBST(LIBGNOMEKBDUI_CFLAGS)
AC_SUBST(LIBGNOMEKBDUI_LIBS)

dnl ==============================================
dnl themus and fontilus dependencies
dnl ==============================================

AC_ARG_ENABLE(vfs-methods,
  AC_HELP_STRING([--disable-vfs-methods],
                 [do not build the fonts:/// and themes:/// uri schemes]),,
  enable_vfs_methods=yes)

if test x$enable_vfs_methods = xyes; then

  PKG_CHECK_MODULES(THEME_METHOD,
  [gnome-vfs-module-2.0 >= 2.2.0 dnl
   gnome-vfs-2.0 >= 2.2.0])
  PKG_CHECK_MODULES(THEMUS_THUMBNAILER,
  [gdk-2.0 >= 2.2.0 dnl
   libgnomeui-2.0 >= 2.2.0 dnl
   libmetacity-private dnl
   gnome-vfs-2.0 >= 2.2.0])
  PKG_CHECK_MODULES(THEMUS_COMPONENT,
  [glib-2.0 >= 2.8.0 dnl
   gtk+-2.0 >= 2.4.0 dnl
   libnautilus-extension >= nautilus_minver])
  PKG_CHECK_MODULES(THEMUS_APPLIER,
  [gtk+-2.0 >= 2.4.0 dnl
   libglade-2.0 >= 2.0 dnl
   gnome-vfs-2.0 >= 2.2.0 dnl
   libgnome-2.0 >= 2.2.0])
  PKG_CHECK_MODULES(FONT_METHOD,
  [gnome-vfs-module-2.0 >= gnomevfs_minver dnl
   fontconfig >= fontconfig_minver])
  PKG_CHECK_MODULES(FONT_THUMBNAILER,
  [gnome-vfs-2.0 >= gnomevfs_minver dnl
   gdk-pixbuf-2.0 >= gtk_minver])
  PKG_CHECK_MODULES(FONT_CONTEXT_MENU,
  [glib-2.0 >= 2.8.0 dnl
   gnome-vfs-2.0 >= gnomevfs_minver dnl
   libnautilus-extension >= nautilus_minver])
  PKG_CHECK_MODULES(FONT_VIEW,
  [gnome-vfs-2.0 >= gnomevfs_minver dnl
   xft >= xft_minver dnl
   gtk+-2.0 >= gtk_minver dnl
   libgnomeui-2.0 >= libgnomeui_minver])

  # Add freetype flags to the thumbnailer script
  AC_PATH_PROG(FREETYPE_CONFIG, freetype-config, no)
  if test "x$FREETYPE_CONFIG" = xno; then
      AC_MSG_ERROR([Could not find freetype-config script])
  fi
  FONT_THUMBNAILER_CFLAGS="$FONT_THUMBNAILER_CFLAGS `$FREETYPE_CONFIG --cflags`"
  FONT_THUMBNAILER_LIBS="$FONT_THUMBNAILER_LIBS `$FREETYPE_CONFIG --libs`"

  THEMUS=themus
  FONTILUS=fontilus
  AC_SUBST(THEMUS)
  AC_SUBST(FONTILUS)

  # Determine the install dir for the nautilus extensions.
  NAUTILUSDIR=`$PKG_CONFIG --variable=extensiondir libnautilus-extension`
  AC_SUBST(NAUTILUSDIR)

  # update LIBS for $export_dynamic
  export_dynamic=`(./libtool --config; echo eval echo \\$export_dynamic_flag_spec) | sh`
  if test -n "$export_dynamic"; then
    THEME_METHOD_LIBS=`echo $THEME_METHOD_LIBS | sed -e "s/$export_dynamic//"`
    THEMUS_COMPONENT_LIBS=`echo $THEMUS_COMPONENT_LIBS | sed -e "s/$export_dynamic//"`
    FONT_VIEW_LIBS=`echo $FONT_VIEW_LIBS | sed -e "s/$export_dynamic//"`
    FONT_CONTEXT_MENU_LIBS=`echo $FONT_CONTEXT_MENU_LIBS | sed -e "s/$export_dynamic//"`
    FONT_THUMBNAILER_LIBS=`echo $FONT_THUMBNAILER_LIBS | sed -e "s/$export_dynamic//"`
    FONT_METHOD_LIBS=`echo $FONT_METHOD_LIBS | sed -e "s/$export_dynamic//"`
  fi
fi

dnl ==============================================
dnl About-me
dnl ==============================================

AC_MSG_CHECKING([whether to enable About Me])
AC_ARG_ENABLE([aboutme],
   AC_HELP_STRING([--enable-aboutme],
                  [enable about-me capplet]),,
   [enable_aboutme=no])
AC_MSG_RESULT([$enable_aboutme])

if test "x$enable_aboutme" = "xyes"; then
  PKG_CHECK_MODULES(LIBEBOOK, [libebook-1.2 >= 1.7.90],
                    [AC_DEFINE([HAVE_LIBEBOOK], 1,
                    [Define if evolution-data-server libebook-1.2 is available])])
fi

AM_CONDITIONAL(BUILD_ABOUTME, test "x$enable_aboutme" = "xyes")

dnl ==============================================
dnl HAL stuff
dnl ==============================================
AC_ARG_ENABLE(hal, AC_HELP_STRING([--disable-hal],[build without hal support]))
if test "x$enable_hal" != "xno"; then
	PKG_CHECK_MODULES(HAL, [hal >= 0.5.6, dbus-1 >= 0.32, dbus-glib-1 >= 0.32],
		[ AC_DEFINE(USE_HAL, 1, [Define if libhal is available]) ],)
	AC_SUBST(HAL_CFLAGS)
	AC_SUBST(HAL_LIBS)
fi

dnl ==============================================
dnl End: Check that we meet the  dependencies
dnl ==============================================

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal, no)

if test x"$GLIB_GENMARSHAL" = xno; then
  AC_MSG_ERROR([glib-genmarshal executable not found in your path - should be installed with glib])
fi

AC_SUBST(GLIB_GENMARSHAL)

dnl ==============================================
dnl Special GConf section
dnl ==============================================

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

dnl ==============================================
dnl Esd section
dnl ==============================================
have_esd=no
AC_ARG_ENABLE(esd,
  AC_HELP_STRING([--disable-esd],
                 [turn off ESD audio support]),
       [case "${enableval}" in
               yes) WANT_ESD=yes ;;
               no)  WANT_ESD=no ;;
               *) AC_MSG_ERROR(bad value ${enableval} for --disable-esd) ;;
       esac],
       [WANT_ESD=yes]) dnl Default value

if test x$WANT_ESD = xyes ; then
       PKG_CHECK_MODULES(ESD, esound >= 0.2.28,
               have_esd=yes
	       AC_DEFINE(HAVE_ESD, 1, [Define if ESD sound server should be used]),
               AC_MSG_RESULT([disabled]))
fi

AC_SUBST(ESD_CFLAGS)
AC_SUBST(ESD_LIBS)

dnl ==============================================
dnl GStreamer section
dnl ==============================================
GST_MAJORMINOR=auto

AC_ARG_ENABLE(gstreamer,
AC_HELP_STRING([--disable-gstreamer],[disable gstreamer support]),
[case "${enableval}" in
 yes) ENABLE_GSTREAMER=yes ;;
 0.10) ENABLE_GSTREAMER=yes && GST_MAJORMINOR=0.10 ;;
 no)  ENABLE_GSTREAMER=no ;;
 *) AC_MSG_ERROR([
                  *** Bad value ${enableval} for --enable-gstreamer
                  *** Please use one of the following:
                  ***    --enable-gstreamer=0.10
               ]) ;;
esac],
[ENABLE_GSTREAMER=yes]) dnl Default value

have_gstreamer=no
if test "x$ENABLE_GSTREAMER" = "xyes"; then
   GST_REQS=0.10.1.2
   PKGS="gstreamer-0.10 >= $GST_REQS gstreamer-plugins-base-0.10 >= $GST_REQS"

   PKG_CHECK_MODULES(GST, $PKGS, have_gstreamer=yes,
                     AC_MSG_RESULT([no]))

   GST_LIBS="$GST_LIBS -lgstinterfaces-0.10 -lgstaudio-0.10"
else
   AC_MSG_NOTICE([*** GStreamer support disabled ***])
fi
AM_CONDITIONAL(HAVE_GSTREAMER, test "x$have_gstreamer" = "xyes")
AC_SUBST(GST_LIBS)
AC_SUBST(GST_CFLAGS)

if test "x$have_gstreamer" = "xyes"; then
  SOUND_CAPPLET_LIBS="$ESD_LIBS $GST_LIBS -lgstaudio-0.10 -lgstinterfaces-0.10"
  SOUND_CAPPLET_CFLAGS="$ESD_CFLAGS $GST_CFLAGS"
  AC_SUBST(SOUND_CAPPLET_LIBS)
  AC_SUBST(SOUND_CAPPLET_CFLAGS)
else
  AC_MSG_NOTICE([*** Sound capplet will not be built]);
fi

dnl ==============================================
dnl Define the main variables
dnl ==============================================
COMMON_CFLAGS="-I\$(top_srcdir)/capplets/common"
COMMON_LIBS="\$(top_builddir)/capplets/common/libcommon.la"

EXTRA_CFLAGS="-I\$(top_srcdir)/ -DG_LOG_DOMAIN=\"\\\"\$(cappletname)-properties\\\"\""

GNOMECC_CAPPLETS_CFLAGS="${COMMON_CFLAGS} ${CAPPLET_CFLAGS} ${EXTRA_CFLAGS} ${DBUS_CFLAGS}"
GNOMECC_CAPPLETS_LIBS="${COMMON_LIBS} ${CAPPLET_LIBS} ${DBUS_LIBS}"

GNOMECC_CAPPLETS_CLEANFILES="\$(desktop) \$(desktop).in"
GNOMECC_CAPPLETS_EXTRA_DIST="ChangeLog \$(desktop).in.in \$(cappletname)-capplet.png \$(cappletname)-properties.glade \$(pixmaps_DATA)"

AC_SUBST(GNOMECC_CAPPLETS_EXTRA_DIST)
AC_SUBST(GNOMECC_CAPPLETS_CLEANFILES)
AC_SUBST(GNOMECC_CAPPLETS_CFLAGS)
AC_SUBST(GNOMECC_CAPPLETS_LIBS)

dnl =======================================
dnl Update Mime Database
dnl =======================================

AC_PATH_PROG(UPDATE_MIME_DATABASE, update-mime-database, no)

AC_ARG_ENABLE(update-mimedb,
   AC_HELP_STRING([--disable-update-mimedb],
                   [do not update mime database after installation]),,
                   enable_update_mimedb=yes)
AM_CONDITIONAL(ENABLE_UPDATE_MIMEDB, test x$enable_update_mimedb = xyes)


AC_CONFIG_FILES([
Makefile
capplets/Makefile
capplets/about-me/Makefile
capplets/about-me/gnome-about-me.desktop.in
capplets/accessibility/Makefile
capplets/accessibility/at-properties/at-properties.desktop.in
capplets/accessibility/at-properties/Makefile
capplets/appearance/Makefile
capplets/appearance/data/Makefile
capplets/appearance/data/gnome-appearance-properties.desktop.in
capplets/appearance/data/gnome-theme-installer.desktop.in
capplets/common/Makefile
capplets/default-applications/Makefile
capplets/default-applications/default-applications.desktop.in
capplets/default-applications/gnome-at-commandline.in
capplets/default-applications/gnome-at-session.desktop.in
capplets/display/Makefile
capplets/display/display-properties.desktop.in
capplets/keybindings/Makefile
capplets/keybindings/gnome-keybindings.pc
capplets/keybindings/keybinding.desktop.in
capplets/keyboard/Makefile
capplets/keyboard/keyboard.desktop.in
capplets/mouse/Makefile
capplets/mouse/gnome-settings-mouse.desktop.in
capplets/network/Makefile
capplets/network/gnome-network-preferences.desktop.in
capplets/sound/Makefile
capplets/sound/gnome-settings-sound.desktop.in
capplets/windows/Makefile
capplets/windows/window-properties.desktop.in
help/Makefile
libbackground/Makefile
libslab/Makefile
libslab/libslab.pc
libsounds/Makefile
libwindow-settings/Makefile
libwindow-settings/gnome-window-settings-2.0.pc
po/Makefile.in
shell/Makefile
shell/gnomecc.desktop.in
typing-break/Makefile
vfs-methods/Makefile
vfs-methods/fontilus/Makefile
vfs-methods/fontilus/gnome-font-viewer.desktop.in
vfs-methods/themus/Makefile
vfs-methods/themus/themus-theme-applier.desktop.in
])

dnl due to a bug in intltool we need to expand something from the root last control-center.spec
AC_OUTPUT
